#+title:      sq
#+date:       [2025-07-15 Tue 16:28]
#+filetags:   :database:
#+identifier: 20250715T162835
* tangle
#+begin_src elisp
(let* ((json (with-temp-buffer
               (call-process "sq" nil t nil "ls" "-cj" "e")
               (goto-char (point-min))
               (condition-case nil
                   (json-parse-buffer)
                 (json-parse-error nil))))
       (sources
        (when json
          (mapcar
           (lambda (s) (cons (gethash "handle" s) (gethash "location" s)))
           (gethash "sources" json))))
       (connections
        (mapcar (lambda (db)
                  (cons (concat "@e/" (downcase (subst-char-in-string ?- ?_ (car db))))
                        (zr-prog-sql-connection-to-uri db)))
                sql-connection-alist))
       (masked
        (mapcar (lambda (db)
                  (cons (car db)
                        (replace-regexp-in-string
                         ":\\([^:@]+?\\)@" "xxxxx"
                         (cdr db) nil nil 1)))
                connections)))
  (when-let* ((removed (cl-set-difference sources masked
                                          :test #'equal)))
    (apply #'call-process "sq" nil nil nil "rm"
           (mapcar #'car removed)))
  (when-let* ((added (cl-set-difference masked sources
                                        :test #'equal)))
    (dolist (conn added)
      (let ((handle (car conn)))
        (call-process "sq" nil nil nil "add" "--skip-verify"
                      "--handle" handle
                      (alist-get handle connections nil nil #'string=))))))
#+end_src
