#+title:      sing-box
#+date:       [2025-08-22 Fri 16:25]
#+filetags:   :network:
#+identifier: 20250822T162554

* tangle
#+begin_src elisp
(dolist (c '("client-config"
             "server-config"
             "android-tun"))
  (zr-org-babel-json-format c))
(let* ((box-secrets (with-temp-buffer
                      (call-process "sops" nil t nil "-d" "sing-box.json")
                      (goto-char 1)
                      (json-parse-buffer)))
       (client-tmp-dir "_tangle/tmp/")
       (tun-dir (expand-file-name "tun/" client-tmp-dir))
       (client-dir "_tangle/client/")
       (server-dir (expand-file-name "_tangle/server/"))
       (hosts-dir "_tangle/hosts/")
       (hosts-uris `(;; ("github" . "https://gitlab.com/ineo6/hosts/-/raw/master/hosts")
                     ("local" . ,(expand-file-name "_hosts"))))
       hosts-files

       (clash-secrets (gethash "clash" box-secrets))
       (clash-subscribes (gethash "subs" clash-secrets))
       (sublink-domain (or (gethash "sublink" clash-secrets)
                           "linuxdo.icmpmiao.cc"))
       (sub-dir "_tangle/sub/")
       (disabled-sub-file "_disabled-sub.eld")
       (disabled-sub (and (file-exists-p disabled-sub-file)
                          (with-temp-buffer
                            (insert-file-contents disabled-sub-file)
                            (read (current-buffer)))))
       (relays '((((type . "socks")
                   (tag . "warp")
                   (server . "127.0.0.1")
                   (server_port . 40000)
                   (version . "5")))))
       nodes
       sub-outbounds
       us-outbounds
       ja-outbounds
       mode-rules-group

       (main-file (expand-file-name "500-main.json" client-dir))
       (tun-file (expand-file-name "500-tun.json" client-dir))
       (rule-sets-dir (expand-file-name "rule-sets/" "_tangle/"))
       (cf-4 "104.19.148.8")
       (cf-6 "2606:4700:e:5b:b68a:ec78:f652:5e89")
       (delete-by-moving-to-trash))
  (delete-directory client-tmp-dir t)
  (mkdir hosts-dir t)
  (when-let* ((sys-hosts (pcase system-type
                           ('windows-nt "C:/Windows/System32/Drivers/etc/hosts")
                           (_ "/etc/hosts")))
              ((file-readable-p sys-hosts)))
    (push sys-hosts hosts-files))
  (dolist (h hosts-uris)
    (let ((f (expand-file-name (car h) hosts-dir))
          (uri (cdr h)))
      (if (file-remote-p uri)
          (call-process "curl" nil 0 nil "-Lo" f (cdr h))
        (when (file-readable-p uri)
          (make-symbolic-link uri f t)))
      (push f hosts-files)))

  (mkdir client-tmp-dir t)
  (mkdir sub-dir t)

  (mapc
   (lambda (sub)
     (when-let* ((name (gethash "id" sub))
                 ((not (member name disabled-sub)))
                 ((gethash "enable" sub)))
       (condition-case nil
           (let* ((sb (gethash "sb" sub))
                  (link (gethash "link" sub))
                  (content
                   (org-file-contents
                    (cond
                     (sb sb)
                     (link (format "https://%s/singbox?config=%s&ua=&selectedRules=%%5B%%5D&customRules=%%5B%%5D"
                                   sublink-domain
                                   (url-hexify-string link))))
                    t))
                  (obj (json-parse-string content :object-type 'alist))
                  (outbounds (alist-get "outbounds" obj nil nil #'string=))
                  (selector (format "s[%s]" name))
                  (urltest (format "u[%s]" name))
                  (out-path (expand-file-name (format "_600-%s.json" name)
                                              client-tmp-dir))
                  tags
                  obds)
             (write-region content nil
                           (expand-file-name
                            (file-name-with-extension name "json")
                            sub-dir))
             (mapc
              (lambda (obd)
                (when-let* ((type (alist-get 'type obd))
                            ((not (or (member type
                                              '("direct" "selector" "urltest"
                                                "block" "dns"))
                                      (member (alist-get 'method obd)
                                              '("chacha20-poly1305"))
                                      (member (alist-get 'type (alist-get 'transport obd))
                                              '("xhttp"))
                                      (and (string= type "tuic")
                                           (not (assoc 'tls obd)))
                                      (assoc 'utls obd)
                                      (assoc 'plugin obd))))
                            (tag (format "%s[%s]" (alist-get 'tag obd) name)))
                  (push tag tags)
                  (push (push `(tag . ,tag) obd) obds)))
              outbounds)
             (if tags
                 (cond
                  ((gethash "relay" sub) (push obds relays))
                  ((gethash "node" sub)
                   (let ((json `((outbounds . ,(vconcat obds)))))
                     (push tags nodes)
                     (write-region (json-serialize json) nil out-path)))
                  (t (let* ((extra `(((type . "selector")
                                      (tag . ,selector)
                                      (outbounds . ,(vconcat (cons urltest tags)))
                                      (default . ,urltest)
                                      (interrupt_exist_connections . :false))
                                     ((type . "urltest")
                                      (tag . ,urltest)
                                      (outbounds . ,(vconcat tags)))))
                            (merged (vconcat extra obds))
                            (json `((outbounds . ,merged))))
                       (push (format "s[%s]" name) sub-outbounds)
                       (write-region (json-serialize json) nil out-path))))
               (message "Sub: %s not work." name)))
         (wrong-type-argument (message "Sub: %s not work." name))
         (json-parse-error (message "Sub: %s json not valid." name))
         (json-end-of-file (message "Sub: %s json not valid." name)))))
   clash-subscribes)

  (cl-letf
      (((symbol-function 'extract-ip)
        (lambda (dir)
          (mkdir dir t)
          (when-let* ((file (car (last (directory-files dir t "^[^.]+\\.csv$")))))
            (with-temp-buffer
              (insert-file-contents file)
              (goto-char (point-min))
              (forward-line)
              (let ((pos (point)))
                (search-forward "," (pos-eol))
                (buffer-substring pos (1- (point)))))))))
    (let ((fmt "../cloudflarest/_results/cf-%d/"))
      (when-let* ((ip (extract-ip (format fmt 4))))
        (setq cf-4 ip))
      (when-let* ((ip (extract-ip (format fmt 6))))
        (setq cf-6 ip))))

  (let* ((dns-remote-server (string-trim (or (org-file-contents "_dns-remote-server" t t)
                                             "127.0.0.1")))
         (dns-remote-ipv6-p (string-match-p ":" dns-remote-server))
         (coding-system-for-write 'utf-8))
    (org-babel-tangle))

  (dolist (f (directory-files "." t "^_.+\\.json$" t))
    (make-symbolic-link f client-tmp-dir t))

  (call-process "sing-box" nil nil nil "merge"
                main-file "-C" client-tmp-dir)
  (when (file-directory-p tun-dir)
    (make-symbolic-link main-file tun-dir t)
    (call-process "sing-box" nil nil nil "merge" tun-file "-C" tun-dir))

  (dolist (f (directory-files rule-sets-dir t "\\.json$" t))
    (let ((out (file-name-with-extension f "srs")))
      (call-process "sing-box" nil nil nil "rule-set" "compile" "--output" out f)
      (when (string-prefix-p "server-" (file-name-base out))
        (make-symbolic-link out server-dir t))))

  (when-let* (((eq 'android system-type))
              (android-dir "/storage/emulated/0/io.nekohasekai.sfa/")
              (media-dir "/storage/emulated/0/Android/media")
              (termux-bin-dir (expand-file-name "com.termux/bin" media-dir))
              (android-target-dir (expand-file-name "io.nekohasekai.sfa/" media-dir)))
    (mkdir android-dir t)
    (dolist (f (directory-files client-dir t "^[^.]" t))
      (unless (file-directory-p f)
        (copy-file f android-dir t)))
    (copy-directory rule-sets-dir android-dir)
    (copy-directory hosts-dir android-dir)
    (zr-android-call-rish
     (string-join
      (list (format "sed -i 's,%s,%s,g' %s/*.json"
                    (expand-file-name "_tangle/")
                    android-target-dir
                    android-dir)
            (format "rm -rf %s/{hosts,rule-sets}"
                    (shell-quote-argument android-target-dir))
            (format "mv -f %s/* %s"
                    (shell-quote-argument android-dir)
                    (shell-quote-argument android-target-dir)))
      " && "))))
#+end_src

* config
:PROPERTIES:
:CUSTOM_ID: 3aeea361-850d-4cc8-b292-065568c194d3
:END:

*** client
:PROPERTIES:
:tangle-dir: _tangle/tmp
:CUSTOM_ID: 4acfcf10-2bef-4815-af7a-fd5f0271c77f
:END:

***** main
:PROPERTIES:
:CUSTOM_ID: fed30130-cdf9-42cb-805c-50dbb7b4c5bf
:END:
#+header: :var rule_sets_dir=(expand-file-name rule-sets-dir)
#+header: :var cache_path=(expand-file-name "cache.db" "_tangle")
#+header: :var jsdelivr="https://fastly.jsdelivr.net"
#+header: :var dns_default="ld-neo"
#+header: :var dns_remote_answer=(format "localhost. IN %s %s" (if dns-remote-ipv6-p "AAAA" "A") dns-remote-server)
#+header: :var clash_secret=(substring (json-serialize (auth-source-pick-first-password :host "clash" :user "secret")) 1 -1)
#+name: client-config
#+begin_src json :tangle (zr-org-by-tangle-dir "_500-main.json") :mkdirp t
{
  "dns": {
    "final": "$dns_default",
    "rules": [
      {
        "action": "predefined",
        "answer": [
          "localhost. IN A 127.0.0.1"
        ],
        "ip_accept_any": true,
        "rcode": "NOERROR",
        "rule_set": "dns-local-rules"
      },
      {
        "action": "predefined",
        "answer": [
          "$dns_remote_answer"
        ],
        "ip_accept_any": true,
        "rcode": "NOERROR",
        "rule_set": "dns-remote-rules"
      },
      {
        "ip_accept_any": true,
        "rule_set": "tailscale-rules",
        "server": "tailscale-dns"
      },
      {
        "action": "reject",
        "rule_set": "geosite-adblockplus"
      },
      {
        "rule_set": "geosite-geolocation-cn",
        "server": "ali-doh"
      },
      {
        "client_subnet": "114.114.114.114/24",
        "mode": "and",
        "rules": [
          {
            "invert": true,
            "rule_set": "geosite-geolocation-!cn"
          },
          {
            "rule_set": "geoip-cn"
          }
        ],
        "server": "$dns_default",
        "type": "logical"
      },
      {
        "rule_set": "geosite-geolocation-!cn",
        "server": "$dns_default"
      }
    ],
    "servers": [
      {
        "domain_resolver": "ali-quic",
        "path": "/dohgo",
        "server": "hk.alpha.abrdns.com",
        "tag": "ld-tuso-hk",
        "type": "https"
      },
      {
        "domain_resolver": "ali-quic",
        "path": "/dnsgo",
        "server": "runtime.webn.cc",
        "server_port": 2083,
        "tag": "ld-tuso-us1",
        "type": "https"
      },
      {
        "domain_resolver": "ali-quic",
        "path": "/dohgo",
        "server": "us.alpha.abrdns.com",
        "tag": "ld-tuso-us2",
        "type": "https"
      },
      {
        "domain_resolver": "ali-quic",
        "path": "/query-dns",
        "server": "ld.ddd.oaifree.com",
        "tag": "ld-neo",
        "type": "https"
      },
      {
        "accept_default_resolvers": false,
        "endpoint": "ts-ep",
        "tag": "tailscale-dns",
        "type": "tailscale"
      },
      {
        "detour": "s[dns]",
        "server": "1.1.1.1",
        "tag": "cf-dot",
        "type": "tls"
      },
      {
        "detour": "s[dns]",
        "server": "8.8.8.8",
        "tag": "google-doh",
        "type": "https"
      },
      {
        "server": "223.5.5.5",
        "tag": "ali-doh",
        "type": "https"
      },
      {
        "server": "223.5.5.5",
        "tag": "ali-quic",
        "type": "quic"
      }
    ],
    "strategy": "prefer_ipv6"
  },
  "experimental": {
    "cache_file": {
      "enabled": true,
      "path": "$cache_path",
      "store_rdrc": false
    },
    "clash_api": {
      "access_control_allow_origin": [
        "https://board.zash.run.place",
        "http://127.0.0.1"
      ],
      "access_control_allow_private_network": true,
      "external_controller": "127.0.0.1:9090",
      "secret": "$clash_secret"
    }
  },
  "log": {
    "level": "info"
  },
  "route": {
    "default_domain_resolver": {
      "server": "$dns_default"
    },
    "final": "direct",
    "rule_set": [
      {
        "path": "$rule_sets_dir/dns-local.srs",
        "tag": "dns-local-rules",
        "type": "local"
      },
      {
        "path": "$rule_sets_dir/dns-remote.srs",
        "tag": "dns-remote-rules",
        "type": "local"
      },
      {
        "path": "$rule_sets_dir/proxy.srs",
        "tag": "proxy-rules",
        "type": "local"
      },
      {
        "path": "$rule_sets_dir/direct.srs",
        "tag": "direct-rules",
        "type": "local"
      },
      {
        "path": "$rule_sets_dir/block.srs",
        "tag": "block-rules",
        "type": "local"
      },
      {
        "path": "$rule_sets_dir/browse.srs",
        "tag": "browse-rules",
        "type": "local"
      },
      {
        "path": "$rule_sets_dir/download.srs",
        "tag": "download-rules",
        "type": "local"
      },
      {
        "path": "$rule_sets_dir/wireguard.srs",
        "tag": "wireguard-rules",
        "type": "local"
      },
      {
        "path": "$rule_sets_dir/tailscale.srs",
        "tag": "tailscale-rules",
        "type": "local"
      },
      {
        "path": "$rule_sets_dir/mitm.srs",
        "tag": "mitm-rules",
        "type": "local"
      },
      {
        "download_detour": "direct",
        "tag": "geoip-telegram",
        "type": "remote",
        "url": "$jsdelivr/gh/chocolate4u/Iran-sing-box-rules@rule-set/geoip-telegram.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geoip-microsoft",
        "type": "remote",
        "url": "$jsdelivr/gh/chocolate4u/Iran-sing-box-rules@rule-set/geoip-microsoft.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geoip-cloudflare",
        "type": "remote",
        "url": "$jsdelivr/gh/chocolate4u/Iran-sing-box-rules@rule-set/geoip-cloudflare.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geoip-cn",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geoip@rule-set/geoip-cn.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geosite-cn",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geosite@rule-set/geosite-cn.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geosite-geolocation-cn",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geosite@rule-set/geosite-geolocation-cn.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geosite-geolocation-!cn",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geosite@rule-set/geosite-geolocation-!cn.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geosite-category-porn",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geosite@rule-set/geosite-category-porn.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geosite-category-ai-!cn",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geosite@rule-set/geosite-category-ai-!cn.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geosite-google",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geosite@rule-set/geosite-google.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geosite-microsoft",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geosite@rule-set/geosite-microsoft.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geosite-talkatone",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geosite@rule-set/geosite-talkatone.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geosite-ebay",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geosite@rule-set/geosite-ebay.srs"
      },
      {
        "download_detour": "direct",
        "tag": "geosite-adblockplus",
        "type": "remote",
        "url": "$jsdelivr/gh/SagerNet/sing-geosite@rule-set/geosite-adblockplus.srs"
      }
    ]
  }
}
#+end_src

***** bounds
:PROPERTIES:
:CUSTOM_ID: c772b10e-46cb-40e1-96e2-70361ae1c337
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "_500-bounds.json")
<<gen-client-bounds()>>
#+end_src

***** route
:PROPERTIES:
:CUSTOM_ID: 7a630887-b412-4cd8-a80f-584867a817e2
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "_500-rules.json")
<<gen-route()>>
#+end_src

#+name: gen-route
#+begin_src elisp
(let* ((pre-rules '(((action . "sniff")
                     (sniffer . ["bittorrent"
                                 "dns"
                                 "ntp"
                                 "stun"]))
                    ((action . "hijack-dns")
                     (mode . "or")
                     (rules . [((protocol . "dns"))
                               ((port . 53))])
                     (type . "logical"))
                    ((outbound . "mitm")
                     (rule_set . "mitm-rules"))
                    ((outbound . "ts-ep")
                     (rule_set . "tailscale-rules"))
                    ((action . "reject")
                     (rule_set . "block-rules"))
                    ((mode . "or")
                     (outbound . "direct")
                     (rules . [((rule_set . ["direct-rules"
                                             "geosite-ebay"]))
                               ((protocol . ["bittorrent"
                                             "ntp"
                                             "stun"]))])
                     (type . "logical"))
                    ((mode . "and")
                     (outbound . "direct")
                     (rules . [((rule_set . "geoip-microsoft"))
                               ((rule_set . "download-rules"))])
                     (type . "logical"))
                    ((mode . "and")
                     (outbound . "s[browse]")
                     (rules . [((rule_set . ["geosite-category-porn"
                                             "geosite-category-ai-!cn"
                                             "geosite-google"
                                             "geosite-talkatone"
                                             "geosite-microsoft"]))
                               ((rule_set . "browse-rules"))])
                     (type . "logical"))
                    ((mode . "and")
                     (outbound . "s[download]")
                     (rules . [((rule_set . ["geosite-category-porn"
                                             "proxy-rules"]))
                               ((rule_set . "download-rules"))])
                     (type . "logical"))
                    ((outbound . "s[this-way]")
                     (rule_set . ["geosite-category-porn"
                                  "geoip-telegram"
                                  "proxy-rules"]))
                    ((action . "resolve"))))
       (post-rules '(((ip_is_private . t)
                      (outbound . "direct")
                      (rule_set . ["geosite-geolocation-cn"
                                   "geosite-cn"
                                   "geoip-cn"]))
                     ((mode . "and")
                      (outbound . "s[this-way]")
                      (rules . [((rule_set . "geosite-geolocation-!cn"))
                                ((invert . t)
                                 (rule_set . "geoip-cn"))])
                      (type . "logical"))))
       (cf-group-rules `(((clash_mode . "cf-direct")
                          (outbound . "direct")
                          (rule_set . "geoip-cloudflare"))
                         ((clash_mode . "cf-v4")
                          (outbound . "direct")
                          (override_address . ,cf-4)
                          (rule_set . "geoip-cloudflare"))
                         ((clash_mode . "cf-v6")
                          (outbound . "direct")
                          (override_address . ,cf-6)
                          (rule_set . "geoip-cloudflare"))))
       (default-mode (cond
                      (zr-sys-android-p "cf-direct/wg-ep[vps2]")
                      ((zr-net-has-public-ipv6-addr-p) "cf-v6/wg-ep[vps2]")
                      (t "cf-v4/wg-ep[vps2]"))))
  (push cf-group-rules mode-rules-group)
  (cl-letf*
      (((symbol-function 'clash--cartesian-product)
        (lambda (lists)
          "Return cartesian product of LISTS (a list of lists).
Example: '((a b) (c d)) => '((a c) (a d) (b c) (b d)).
Order is preserved."
          (let ((res (list nil)))
            (dolist (lst lists res)
              (setq res
                    (cl-mapcan
                     (lambda (prefix)
                       (mapcar (lambda (x) (append prefix (list x))) lst))
                     res))))))
       ((symbol-function 'clash-expand-clash-mode-combinations)
        (lambda (groups &optional sep)
          "Expand GROUPS by combining `clash_mode` across all groups (cartesian product).

GROUPS is a list of groups; each group is a list of rule alists.
Each rule must have (clash_mode . STRING).

Result keeps the same number of groups. For each original group i:
- Every rule r in group i is duplicated for every combination of `clash_mode`
  picked from all other groups.
- The new rule keeps all fields from r, only updating `clash_mode` to the
  concatenation of the selected modes across ALL groups, in original group order.

SEP defaults to \"/\"."
          (setq sep (or sep "/"))
          (let* ((n (length groups))
                 (modes-per-group
                  (mapcar (lambda (g)
                            (mapcar (lambda (rule)
                                      (alist-get 'clash_mode rule))
                                    g))
                          groups)))
            (cl-loop
             for i from 0 below n
             collect
             (let* ((this-group (nth i groups))
                    (other-modes
                     (cl-loop for j from 0 below n
                              unless (= j i)
                              collect (nth j modes-per-group)))
                    (combos (clash--cartesian-product other-modes)))
               (cl-loop
                for rule in this-group
                append
                (let ((this-mode (alist-get 'clash_mode rule)))
                  (cl-loop
                   for combo in combos
                   collect
                   (let* ((k 0)
                          (all-modes
                           (cl-loop for j from 0 below n
                                    collect (if (= j i)
                                                this-mode
                                              (prog1 (nth k combo)
                                                (setq k (1+ k))))))
                          (new-rule (copy-tree rule t)))
                     (setf (alist-get 'clash_mode new-rule)
                           (mapconcat #'identity all-modes sep))
                     new-rule))))))))))
    (let* ((mode-rules (apply #'vconcat (clash-expand-clash-mode-combinations
                                         mode-rules-group))))
      (json-serialize `((experimental (clash_api (default_mode . ,default-mode)))
                        (route (rules . ,(vconcat pre-rules mode-rules post-rules))))))))
#+end_src

***** hosts
:PROPERTIES:
:CUSTOM_ID: b8b405ea-5649-4bb3-9abd-ab60a0332b85
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "_400-hosts.json")
<<gen-hosts()>>
#+end_src

#+name: gen-hosts
#+begin_src elisp
(let* ((default '((localhost . ["127.0.0.1" "::1"])))
       (tag "local-hosts")
       (hosts (vconcat hosts-files))
       (local-file "_hosts.eld")
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer))))))
  (json-serialize
   `((dns . ((servers . [((tag . ,tag)
                          (type . "hosts")
                          (path . ,hosts)
                          (predefined . ,(append local default)))])
             (rules . [((ip_accept_any . t)
                        (server . ,tag))]))))))
#+end_src

***** platform

***** pc
:PROPERTIES:
:CUSTOM_ID: cf7be985-bfaa-4ed0-8240-190e588c1fd2
:END:
#+begin_src json :tangle (if (eq system-type 'android) "no" (zr-org-by-tangle-dir "_400-dns.json"))
{
  "inbounds": [
    {
      "listen": "::1",
      "listen_port": 53,
      "network": "udp",
      "tag": "dns6-in",
      "type": "direct"
    },
    {
      "listen": "127.0.0.1",
      "listen_port": 53,
      "network": "udp",
      "tag": "dns4-in",
      "type": "direct"
    }
  ]
}
#+end_src

***** tun
:PROPERTIES:
:tangle-dir: (concat tun-dir)
:END:

***** android
:PROPERTIES:
:CUSTOM_ID: 2970e9bb-61e8-4eb3-bc19-233858560385
:END:
#+name: android-tun
#+begin_src json :tangle (if (eq system-type 'android) (zr-org-by-tangle-dir "500-android.json") "no") :mkdirp t
{
  "inbounds": [
    {
      "address": [
        "172.19.0.1/30",
        "fdfe:dcba:9876::1/126"
      ],
      "auto_route": true,
      "mtu": 9000,
      "platform": {
        "http_proxy": {
          "enabled": true,
          "server": "127.0.0.1",
          "server_port": 10807
        }
      },
      "stack": "system",
      "strict_route": true,
      "type": "tun"
    }
  ],
  "route": {
    "auto_detect_interface": true
  }
}
#+end_src

***** linux
:PROPERTIES:
:CUSTOM_ID: 17b75a2d-d55b-4d9d-b248-50ddbb61fdd9
:END:
#+name: linux-tun
#+begin_src json :tangle (if (eq system-type 'gnu/linux) (zr-org-by-tangle-dir "400-linux.json") "no") :mkdirp t
{
  "inbounds": [
    {
      "address": [
        "172.19.0.1/30",
        "fdfe:dcba:9876::1/126"
      ],
      "auto_route": true,
      "auto_redirect": true,
      "mtu": 9000,
      "route_exclude_address_set": ["geoip-cn"],
      "stack": "system",
      "strict_route": true,
      "tag": "tun-in",
      "type": "tun"
    }
  ],
  "route": {
    "final": "s[this-way]",
    "rules": [
      {
        "inbound": "tun-in",
        "outbound": "direct",
        "process_name": [ "sing-box" ]
      }
    ],
    "auto_detect_interface": true
  }
}
#+end_src

***** log timestamp
:PROPERTIES:
:CUSTOM_ID: cf1faa67-36f3-4e44-bec2-312bac3dd217
:END:
#+begin_src json :tangle (if (eq system-type 'gnu/linux) "no" (zr-org-by-tangle-dir "_500-log.json"))
{
  "log": {
    "timestamp": true
  }
}
#+end_src

*** server
:PROPERTIES:
:tangle-dir: (concat server-dir)
:END:

***** main
:PROPERTIES:
:CUSTOM_ID: b85ab91b-1175-4b51-9f3c-f37a0b589979
:END:

#+header: :var rule_sets_dir="/etc/sing-box"
#+name: server-config
#+begin_src json :tangle (zr-org-by-tangle-dir "500-main.json") :mkdirp t
{
  "dns": {
    "final": "cf-dot",
    "servers": [
      {
        "accept_default_resolvers": false,
        "endpoint": "ts-ep",
        "tag": "tailscale-dns",
        "type": "tailscale"
      },
      {
        "server": "1.1.1.1",
        "tag": "cf-dot",
        "type": "tls"
      }
    ],
    "strategy": "prefer_ipv6"
  },
  "log": {
    "level": "info"
  },
  "route": {
    "auto_detect_interface": true,
    "default_domain_resolver": {
      "server": "cf-dot"
    },
    "final": "direct",
    "rule_set": [
      {
        "download_detour": "direct",
        "tag": "geoip-cn",
        "type": "remote",
        "url": "https://github.com/SagerNet/sing-geoip/raw/refs/heads/rule-set/geoip-cn.srs"
      }
    ],
    "rules": [
      {
        "inbound": "dict-in",
        "outbound": "direct",
        "override_address": "127.0.0.1",
        "override_port": 5628,
        "rule_set": "geoip-cn",
        "rule_set_ip_cidr_match_source": true
      },
      {
        "action": "sniff",
        "sniffer": "dns"
      },
      {
        "action": "hijack-dns",
        "protocol": "dns"
      },
      {
        "inbound": "dot-in",
        "outbound": "direct",
        "override_address": "127.0.0.1",
        "override_port": 8853,
        "rule_set": "geoip-cn",
        "rule_set_ip_cidr_match_source": true
      }
    ]
  }
}
#+end_src

***** server0
:PROPERTIES:
:CUSTOM_ID: bc0f24ed-8894-45b6-9008-4a5f1879b0f1
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "500-s0.json")
<<gen-server-bounds(name="vps0")>>
#+end_src

***** server1
:PROPERTIES:
:CUSTOM_ID: 24c5c785-b594-4987-b947-a22e63c24f3e
:END:

#+begin_src json :tangle (zr-org-by-tangle-dir "500-s1.json")
<<gen-server-bounds(name="vps1")>>
#+end_src

***** server2
:PROPERTIES:
:CUSTOM_ID: dafd128d-c905-49be-a432-42aff4d2159d
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "500-s2.json")
<<gen-server-bounds(name="vps2")>>
#+end_src

* bounds
:PROPERTIES:
:header-args:elisp: :var vps-secrets=(gethash "vps" box-secrets) wg-clients=(gethash "wg" box-secrets) ts-ep='((tag . "ts-ep") (type . "tailscale")) obds='(((tag . "direct") (type . "direct"))) ibds='() ep='() conf='() dns-rules='()
:END:
*** client
#+name: gen-client-bounds
#+begin_src elisp
(let ((tailscale-state-dir (expand-file-name "tailscale_state" "_tangle"))
      (download-tags '("s[this-way]" "direct"))
      (browse-tags '("s[this-way]" "direct" "s[relay]"))
      relay-tags
      wg-rules-group
      s-tags)
  (push `(state_directory . ,tailscale-state-dir) ts-ep)

  (unless (file-exists-p "_500-inbounds.json")
    (push '((listen_port . 10807)
            (set_system_proxy . :false)
            (tag . "mixed-in")
            (type . "mixed"))
          ibds))

  ;; misc outbounds
  (push '((server . "127.0.0.1")
          (server_port . 8080)
          (tag . "mitm")
          (type . "http"))
        obds)

  ;; vps outbounds
  (maphash
   (lambda (name attr)
     (let* ((user-name (gethash "user-name" attr))
            (user-pass (gethash "user-pass" attr))
            (ip-type (if (and (zr-net-has-public-ipv6-addr-p)
                              (gethash "ipv6" attr))
                         "ipv6"
                       "ip"))
            (ip (gethash ip-type attr))
            (u-tag (format "u[%s]" name))
            (s-tag (format "s[%s]" name))
            vps-tags)
       (pcase (gethash "type" attr)
         ("download" (push s-tag download-tags))
         ("browse" (push s-tag browse-tags)))

       (when-let* ((port (gethash "nv-port" attr))
                   (host (gethash "nv-host" attr))
                   (local-port (gethash "nv-local-port" attr))
                   (tag (format "nv[%s]" name))
                   (listen (format "http://127.0.0.1:%d" local-port))
                   (proxy (format "quic://%s:%s@%s" user-name user-pass host))
                   (naive-dir "_tangle/naive/")
                   (host-resolver-rules (format "MAP %s %s" host
                                                (if (eq ip-type :ipv6)
                                                    (format "[%s]" ip)
                                                  ip))))
         (push tag vps-tags)
         (mkdir naive-dir t)
         (write-region (json-serialize `((listen . ,listen)
                                         (proxy . ,proxy)
                                         (host-resolver-rules . ,host-resolver-rules)))
                       nil (expand-file-name name naive-dir))
         (push `((server . "127.0.0.1")
                 (server_port . ,local-port)
                 (tag . ,tag)
                 (type . "http"))
               obds))

       (when-let* ((port (gethash "vl-port" attr))
                   (tag (format "vl[%s]" name)))
         (push tag vps-tags)
         (push `((flow . "xtls-rprx-vision")
                 (server . ,ip)
                 (server_port . ,port)
                 (tag . ,tag)
                 (tls . ((enabled . t)
                         (reality . ((enabled . t)
                                     (public_key . ,(gethash "pub-key" attr))
                                     (short_id . ,(gethash "short-id" attr))))
                         (server_name . ,(gethash "vl-host" attr))
                         (utls . ((enabled . t)))))
                 (uuid . ,(gethash "user-uuid" attr))
                 (type . "vless"))
               obds))

       (when-let* ((port (gethash "any-port" attr))
                   (tag (format "any[%s]" name)))
         (push tag vps-tags)
         (push `((password . ,user-pass)
                 (server . ,ip)
                 (server_port . ,port)
                 (tag . ,tag)
                 (tls . ((enabled . t)
                         (server_name . ,(gethash "any-host" attr))))
                 (type . "anytls"))
               obds))

       (when-let* ((port (gethash "hy-port" attr))
                   (tag (format "hy[%s]" name)))
         (push tag vps-tags)
         (push `((obfs . ((password . ,(gethash "obfs-pass" attr))
                          (type . "salamander")))
                 (password . ,user-pass)
                 (server . ,ip)
                 (server_port . ,port)
                 (tag . ,tag)
                 (tls . ((alpn . ["h3"])
                         (enabled . t)
                         (server_name . ,(gethash "hy-host" attr))))
                 (type . "hysteria2"))
               obds))

       (when vps-tags
         (push `((interrupt_exist_connections . :false)
                 (outbounds . ,(vconcat vps-tags))
                 (tag . ,u-tag)
                 (type . "urltest"))
               obds)
         (push `((default . ,u-tag)
                 (interrupt_exist_connections . :false)
                 (outbounds . ,(vconcat (cons u-tag vps-tags)))
                 (tag . ,s-tag)
                 (type . "selector"))
               obds)
         (dolist (group relays)
           (dolist (relay group)
             (let ((tag (format "%s[%s]" (alist-get 'tag relay) name)))
               (push (append `((tag . ,tag)
                               (detour . ,s-tag))
                             relay)
                     obds)
               (push tag relay-tags))))
         (push s-tag s-tags))

       (when-let* ((sys-name (string-trim (or (org-file-contents "_system-name.txt" t t)
                                              (system-name))))
                   (wg (gethash sys-name wg-clients))
                   (port (gethash "wg-port" attr))
                   (addr (gethash "wg-addr" attr))
                   (pub (gethash "wg-pub-key" attr))
                   (proto (if (and (eq ip-type :ipv6)
                                   (not zr-sys-android-p))
                              "ipv6"
                            "ip"))
                   (peers `( :address ,(gethash proto attr)
                             :port ,port
                             :persistent_keepalive_interval 30
                             :public_key ,pub
                             :allowed_ips ,addr))
                   (tag (format "wg-ep[%s]" name)))
         (push `((clash_mode . ,tag)
                 (outbound . ,tag)
                 (rule_set . "wireguard-rules"))
               wg-rules-group)
         (push `( (tag . ,tag)
                  (name . ,tag)
                  (type . "wireguard")
                  (system . ,(if zr-sys-linux-p t :false))
                  (address . ,(concat (gethash "addr" wg) "/24"))
                  (private_key . ,(auth-source-pick-first-password
                                   :host "wireguard" :user sys-name))
                  (peers . ,(vector peers)))
               ep))))
   vps-secrets)

  (when wg-rules-group
    (push wg-rules-group mode-rules-group))

  ;; selector
  (let* ((u-self-tag "u[self]")
         (s-self-tag "s[self]")
         (u-this-way "u[this-way]")
         (s-this-way (list s-self-tag))
         (u-sub "u[sub]")
         (s-sub (cons u-sub sub-outbounds))
         (u-us "u{us}")
         (s-us (cons u-us us-outbounds))
         (u-dns "u[dns]")
         (s-dns (cons u-dns '("s[this-way]" "s[self]" "direct")))
         pre-obds
         s-region
         region-obds)

    (when s-tags
      (push `((interrupt_exist_connections . :false)
              (outbounds . ,(vconcat s-tags))
              (tag . ,u-self-tag)
              (type . "urltest"))
            obds)
      (push `((default . ,u-self-tag)
              (interrupt_exist_connections . :false)
              (outbounds . ,(vconcat (cons u-self-tag s-tags)))
              (tag . ,s-self-tag)
              (type . "selector"))
            obds))

    (when sub-outbounds
      (push "s[sub]" s-this-way)
      (push "s[sub]" browse-tags)
      (push "s[sub]" s-dns)
      (push `(((type . "selector")
               (tag . "s[sub]")
               (outbounds . ,(vconcat s-sub))
               (default . ,u-sub))
              ((type . "urltest")
               (tag . ,u-sub)
               (outbounds . ,(vconcat (cdr s-sub)))))
            pre-obds))
    (when-let* ((region-outbounds (cl-remove nil `(("us" . ,us-outbounds)
                                                   ("ja" . ,ja-outbounds))
                                             :key #'cdr))
                (region-tag "s[region]"))
      (push region-tag s-this-way)
      (dolist (region region-outbounds)
        (let ((tag (format "u{%s}" (car region))))
          (push tag s-region)
          (push `((type . "urltest")
                  (tag . ,tag)
                  (outbounds . ,(vconcat (cdr region))))
                region-obds)))
      (push `((type . "selector")
              (tag . ,region-tag)
              (outbounds . ,(vconcat s-region)))
            region-obds)
      (write-region (json-serialize `((outbounds . ,(vconcat region-obds))))
                    nil (expand-file-name "_900-region.json" client-tmp-dir)))

    (when relay-tags
      (let ((w-tag "s[relay]"))
        (push `(((type . "selector")
                 (tag . ,w-tag)
                 (outbounds . ,(vconcat relay-tags))))
              pre-obds)
        (push w-tag s-this-way)))
    
    (push `(((type . "selector")
             (tag . "s[this-way]")
             (outbounds . ,(vconcat (cons u-this-way s-this-way)
                                    (apply #'append nodes)))
             (default . ,u-this-way))
            ((type . "urltest")
             (tag . ,u-this-way)
             (outbounds . ,(vconcat s-this-way))))
          pre-obds)
    (push `(((type . "selector")
             (tag . "s[download]")
             (outbounds . ,(vconcat download-tags))
             (default . ,(car download-tags)))
            ((type . "selector")
             (tag . "s[browse]")
             (outbounds . ,(vconcat browse-tags))
             (default . ,(car browse-tags)))
            ((type . "selector")
             (tag . "s[dns]")
             (outbounds . ,(vconcat s-dns))
             (default . ,u-dns))
            ((type . "urltest")
             (tag . ,u-dns)
             (outbounds . ,(vconcat (seq-difference s-dns `(,u-dns "direct"))))))
          pre-obds)
    (setq obds (append (apply #'append pre-obds) obds)))

  ;; bind-interface
  (when-let* ((default-interface-file (expand-file-name "_default-interface"))
              (default-interface (and (file-readable-p default-interface-file)
                                      (string-trim (org-file-contents default-interface-file t t)))))
    (push (cons 'bind_interface default-interface) ts-ep)
    (setq obds
          (mapcar (lambda (obd)
                    (if (or (assoc 'bind_interface obd)
                            (string= (alist-get 'server obd) "127.0.0.1")
                            (member (alist-get 'type obd) '("urltest" "selector")))
                        obd
                      (cons (cons 'bind_interface default-interface) obd)))
                  obds))))
<<return-bounds>>
#+end_src

*** server
#+name: gen-server-bounds
#+begin_src elisp
(push '(advertise_exit_node . t) ts-ep)
(let* ((attr (gethash name vps-secrets))
       (user-name (gethash "user-name" attr))
       (user-pass (gethash "user-pass" attr))
       (email (gethash "email" attr))
       (cf-token (auth-source-pick-first-password :host "dash.cloudflare.com"
                                                  :user (concat "token-" name)))
       wg-peers)

  (when-let* ((ip (gethash "ip" attr)))
    (push `((listen . "::")
            (listen_port . 2628)
            (network . "tcp")
            (tag . "dict-in")
            (type . "direct"))
          ibds)
    (push `((listen . "127.0.0.1")
            (listen_port . 53)
            (network . "udp")
            (tag . "dns4-in")
            (type . "direct"))
          ibds)
    (push `((listen . "::")
            (listen_port . 853)
            (network . "tcp")
            (tag . "dot-in")
            (type . "direct"))
          ibds))

  (maphash
   (lambda (k v)
     (push `( :public_key ,(gethash "pub" v)
              :allowed_ips ,(concat (gethash "addr" v) "/32"))
           wg-peers))
   wg-clients)
  (when-let* ((port (gethash "wg-port" attr))
              (addr (gethash "wg-addr" attr))
              (priv-key (auth-source-pick-first-password :host "wireguard" :user name)))
    (push `((tag . "wg-ep")
            (type . "wireguard")
            (system . t)
            (address . ,addr)
            (listen_port . ,port)
            (private_key . ,priv-key)
            (peers . ,(vconcat wg-peers)))
          ep))

  (when-let* ((port (gethash "vl-port" attr))
              (host (gethash "vl-host" attr))
              (tag (format "vl[%s]" name)))
    (push `((listen . "::")
            (listen_port . ,port)
            (tag . ,tag)
            (tls . ((enabled . t)
                    (reality . ((enabled . t)
                                (handshake . ((server . ,host)
                                              (server_port . 443)))
                                (private_key . ,(gethash "priv-key" attr))
                                (short_id . [,(gethash "short-id" attr)])))
                    (server_name . ,host)))
            (type . "vless")
            (users . [((flow . "xtls-rprx-vision")
                       (name . ,user-name)
                       (uuid . ,(gethash "user-uuid" attr)))]))
          ibds))

  (when-let* ((port (gethash "any-port" attr))
              (host (gethash "any-host" attr))
              (tag (format "any[%s]" name)))
    (push `((listen . "::")
            (listen_port . ,port)
            (tls . ((acme . ((dns01_challenge . ((api_token . ,cf-token)
                                                 (provider . "cloudflare")))
                             (domain . ,host)
                             (email . ,email)))
                    (alpn . ["h3"])
                    (enabled . t)
                    (server_name . ,host)))
            (tag . ,tag)
            (type . "anytls")
            (users . [((password . ,user-pass))]))
          ibds))

  (when-let* ((port (gethash "hy-port" attr))
              (host (gethash "hy-host" attr))
              (tag (format "hy[%s]" name)))
    (push `((down_mbps . 500)
            (listen . "::")
            (listen_port . ,port)
            (masquerade . ((type . "string")
                           (status_code . 403)))
            (obfs . ((password . ,(gethash "obfs-pass" attr))
                     (type . "salamander")))
            (tls . ((acme . ((dns01_challenge . ((api_token . ,cf-token)
                                                 (provider . "cloudflare")))
                             (domain . ,host)
                             (email . ,email)))
                    (alpn . ["h3"])
                    (enabled . t)
                    (server_name . ,host)))
            (tag . ,tag)
            (type . "hysteria2")
            (up_mbps . 500)
            (users . [((password . ,user-pass)
                       (name . ,user-name))]))
          ibds))

  (when-let* ((port (gethash "nv-port" attr))
              (tag (format "nv[%s]" name)))
    (push `((listen . "::")
            (listen_port . ,port)
            (tls . ((acme . ((dns01_challenge . ((api_token . ,cf-token)
                                                 (provider . "cloudflare")))
                             (domain . ,(gethash "nv-host" attr))
                             (email . ,email)))
                    (enabled . t)))
            (tag . ,tag)
            (type . "naive")
            (users . [((password . ,user-pass)
                       (username . ,user-name))]))
          ibds)))
<<return-bounds>>
#+end_src

*** return
#+name: return-bounds
#+begin_src elisp
(push ts-ep ep)
(when dns-rules (push `(dns . (rules ,(vconcat dns-rules))) conf))
(when ep (push `(endpoints . ,(vconcat ep)) conf))
(when obds (push `(outbounds . ,(vconcat obds)) conf))
(when ibds (push `(inbounds . ,(vconcat ibds)) conf))
(json-serialize conf)
#+end_src
* rule-sets
:PROPERTIES:
:tangle-dir: (expand-file-name rule-sets-dir)
:END:
https://sing-box.sagernet.org/zh/configuration/rule-set/headless-rule/

** proxy
:PROPERTIES:
:CUSTOM_ID: 86dd13c4-1322-4298-a426-d3bbed2b8a44
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "proxy.json") :mkdirp t
<<gen-proxy-rule-set()>>
#+end_src

#+name: gen-proxy-rule-set
#+begin_src elisp
(let* ((rules (zr-net-read-proxy-rules))
       (proxy (gethash "proxy" rules))
       (hosts (gethash "autoproxy_hosts" rules))
       (local-file "_proxy.eld")
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer)))))
       (default [])
       domain
       domain-regex
       domain-suffix
       pac)
  (cl-mapc
   (lambda (p h)
     (mapc
      (lambda (d)
        (if (seq-contains-p d ?*)
            (push (string-replace "\\*" ".*" (regexp-quote d)) domain-regex)
          (push d domain)
          (push (concat "." d) domain-suffix)))
      (cl-remove "local" h :test #'string= :count 1)))
   proxy hosts)
  (json-serialize
   `((version . 3)
     (rules . ,(vconcat local default
                        (vector `((domain . ,(vconcat domain))
                                  (domain_suffix . ,(vconcat domain-suffix))
                                  (domain_regex . ,(vconcat domain-regex)))))))))
#+end_src

** direct
:PROPERTIES:
:CUSTOM_ID: f436f071-b706-45c3-a131-db6e6e84d786
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "direct.json") :mkdirp t
<<gen-direct-rule-set()>>
#+end_src

#+name: gen-direct-rule-set
#+begin_src elisp
(let* ((default `(((ip_cidr
                    . ,(vector (format "%s/%s" dns-remote-server (if dns-remote-ipv6-p "128" "32")))))
                  ((process_name
                    . ["cfst"
                       "cloudflared"]))))
       (local-file "_direct.eld")
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer))))))
  (json-serialize
   `((version . 3)
     (rules . ,(vconcat local default)))))
#+end_src

** block
:PROPERTIES:
:CUSTOM_ID: 07161900-feaf-46fe-af4f-5dde91b3d716
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "block.json") :mkdirp t
<<gen-block-rule-set()>>
#+end_src

#+name: gen-block-rule-set
#+begin_src elisp
(let* ((default `(((process_path_regex
                    . ["^D:\\\\Games\\\\.*"]))))
       (local-file "_block.eld")
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer))))))
  (json-serialize
   `((version . 3)
     (rules . ,(vconcat local default)))))
#+end_src

** mitm
:PROPERTIES:
:CUSTOM_ID: 6e15e6bb-74d3-4b61-8cc5-a6a733cc9eec
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "mitm.json") :mkdirp t
<<gen-mitm-rule-set()>>
#+end_src

#+name: gen-mitm-rule-set
#+begin_src elisp
(let* ((place-holder [((domain_suffix . [".it-just-a-placeholder"]))])
       (local-file "_mitm.eld")
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer))))))
  (json-serialize
   `((version . 3)
     (rules . [((type . "logical")
                (mode . "and")
                (rules
                 . ,(vconcat [((process_name
                                . ["mitmproxy"
                                   "mitmproxy.exe"])
                               (invert . t))]
                             (or local place-holder))))]))))
#+end_src

** browse
:PROPERTIES:
:CUSTOM_ID: 6cf04f09-9f9c-43c6-b54c-5c087842b0c5
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "browse.json") :mkdirp t
<<gen-browse-rule-set()>>
#+end_src

#+name: gen-browse-rule-set
#+begin_src elisp
(let* ((local-file "_browse.eld")
       (process-base '("chrome"
                       "cromite"
                       "cli-proxy-api"
                       "firefox"
                       "msedge"
                       "msedgewebview2"))
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer))))))
  (json-serialize
   `((version . 3)
     (rules . ,(vconcat `(((process_name
                            . ,(vconcat (mapcan (lambda (a) (list a (concat a ".exe"))) process-base))))
                          ((package_name
                            . ["com.android.vending"
                               "com.android.hotwordenrollment.xgoogle"
                               "com.android.hotwordenrollment.okgoogle"
                               "com.google.android.gms"
                               "com.google.android.gms.location.history"
                               "com.google.android.gm"
                               "com.google.android.googlequicksearchbox"
                               "com.talkatone.android"
                               "com.google.android.youtube"])))
                        local)))))
#+end_src

** download
:PROPERTIES:
:CUSTOM_ID: 7c609bc0-32ab-40dd-a48a-a330001c2f9d
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "download.json") :mkdirp t
<<gen-download-rule-set()>>
#+end_src

#+name: gen-download-rule-set
#+begin_src elisp
(let* ((local-file "_download.eld")
       (process-base '("aria2c"
                       "mpv"
                       "rclone"
                       "OpenList"))
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer))))))
  (json-serialize
   `((version . 3)
     (rules . ,(vconcat `(((process_name
                            . ,(vconcat (mapcan (lambda (a) (list a (concat a ".exe"))) process-base)))))
                        local)))))
#+end_src

** wireguard
:PROPERTIES:
:CUSTOM_ID: d8aff9df-7c29-46e7-8d01-74c5795c9be6
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "wireguard.json") :mkdirp t
<<gen-wireguard-rule-set()>>
#+end_src

#+name: gen-wireguard-rule-set
#+begin_src elisp
(let* ((place-holder [((ip_cidr . "10.0.0.0/24"))])
       (local-file "_wireguard.eld")
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer))))))
  (json-serialize
   `((version . 3)
     (rules . ,(or local place-holder)))))
#+end_src

** tailscale
:PROPERTIES:
:CUSTOM_ID: 614dce44-209a-4405-9eff-47a6edc61ed8
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "tailscale.json") :mkdirp t
<<gen-tailscale-rule-set()>>
#+end_src

#+name: gen-tailscale-rule-set
#+begin_src elisp
(let* ((place-holder [((domain_suffix . [".ts.net"])
                       (ip_cidr . "100.64.0.0/10"))])
       (local-file "_tailscale.eld")
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer))))))
  (json-serialize
   `((version . 3)
     (rules . ,(or local place-holder)))))
#+end_src

** dns-local
:PROPERTIES:
:CUSTOM_ID: a02fb290-ed0d-45d4-9b62-a86d148727bf
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "dns-local.json") :mkdirp t
<<gen-dns-local-rules()>>
#+end_src

#+name: gen-dns-local-rules
#+begin_src elisp
(let* ((default (vector
                 `(,(cons 'domain_suffix
                          (vector
                           (concat (downcase (system-name)) ".local")))
                   (source_ip_cidr
                    . ["127.0.0.1/32"
                       "::1/128"]))))
       (local-file "_dns-local.eld")
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer))))))
  (json-serialize
   `((version . 3)
     (rules . ,(vconcat local default)))))
#+end_src

** dns-remote
:PROPERTIES:
:CUSTOM_ID: c4683997-5c7f-4181-bed1-743ffc858cb6
:END:
#+begin_src json :tangle (zr-org-by-tangle-dir "dns-remote.json") :mkdirp t
<<gen-dns-remote-rules()>>
#+end_src

#+name: gen-dns-remote-rules
#+begin_src elisp
(let* ((default [((domain_suffix . [".it-just-a-dns-remote-placeholder"]))])
       (local-file "_dns-remote.eld")
       (local (and (file-exists-p local-file)
                   (with-temp-buffer
                     (insert-file-contents local-file)
                     (read (current-buffer))))))
  (json-serialize
   `((version . 3)
     (rules . ,(vconcat local default)))))
#+end_src

